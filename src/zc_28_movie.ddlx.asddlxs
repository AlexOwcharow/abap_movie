@Metadata.layer: #CUSTOMER

@UI.headerInfo: { typeName: 'Movie',
                  typeNamePlural: 'Movies',
                  title.value: 'Title',
                  description.value: 'PublishingYear',
                  imageUrl: 'ImageUrl' }

@UI.lineItem: [ { criticality: 'AverageRatingCriticality' } ]
@UI.presentationVariant: [ { sortOrder: [ { by: 'AverageRating', direction: #DESC } ] } ]

annotate entity ZC_28_Movie
  with

{
  /* Facets */
  @UI.facet: [ { position: 10, type: #FIELDGROUP_REFERENCE, label: 'Movie Details', targetQualifier: 'Details' },
               { position: 20, type: #FIELDGROUP_REFERENCE, label: 'Administrative Data', targetQualifier: 'Admin' },
               { position: 30, type: #LINEITEM_REFERENCE, label: 'Ratings', targetElement: '_Ratings' } ]

  /* Fields */
  @UI.fieldGroup: [ { position: 10, qualifier: 'Admin' } ]
  MovieUuid;

  @UI.fieldGroup: [ { position: 10, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 10 } ]
  Title;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 10 } ]
  Genre;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 20 } ]
  PublishingYear;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 40 } ]
  @UI.selectionField: [ { position: 20 } ]
  RuntimeInMin;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 5 } ]
  ImageUrl;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Admin' } ]
  CreatedAt;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Admin' } ]
  CreatedBy;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Admin' } ]
  LastChangedAt;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Admin' } ]
  LastChangedBy;

  @UI.dataPoint: { visualization: #NUMBER, criticality: 'AverageRatingCriticality' }
  @UI.lineItem: [ { position: 999, criticality: 'AverageRatingCriticality' } ]
  @UI.selectionField: [ { position: 40 } ]
  AverageRating;

  @UI.hidden: true
  AverageRatingCriticality;
}